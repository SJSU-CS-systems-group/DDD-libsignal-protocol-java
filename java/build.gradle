plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

archivesBaseName = "signal-protocol-java"
version = version_number
group = group_info

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    main {
        proto {
            srcDir '../protobuf'
        }
    }
    test {
        java {
            srcDirs = ['src/test/java/', project(':tests').file('src/test/java')]
        }
    }
}

dependencies {
    implementation "org.whispersystems:curve25519-java:${curve25519_version}"
    implementation 'com.google.protobuf:protobuf-javalite:3.10.0'

    testImplementation 'junit:junit:3.8.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

tasks.register('packageJavadoc', Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

tasks.register('packageSources', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives(tasks.named('packageJavadoc'))
    archives(tasks.named('packageSources'))
}

def isReleaseBuild() {
 !version.toString().contains("SNAPSHOT")
}

def getReleaseRepositoryUrl() {
  hasProperty('sonatypeRepo') ?
        sonatypeRepo : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getRepositoryUsername() {
  hasProperty('whisperSonatypeUsername') ? whisperSonatypeUsername : ""
}

def getRepositoryPassword() {
  hasProperty('whisperSonatypePassword') ? whisperSonatypePassword : ""
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact tasks.named('packageJavadoc')
            artifact tasks.named('packageSources')

            groupId group_info
            artifactId archivesBaseName
            version version_number

pom {
    setName("signal-protocol-java")
    setDescription("Signal Protocol cryptography library for Java")
    setUrl("https://github.com/WhisperSystems/libsignal-android")

    licenses {
        license {
            setName("GPLv3")
            setUrl("https://www.gnu.org/licenses/gpl-3.0.txt")
            setDistribution("repo")
        }
    }

    developers {
        developer {
            setName("Moxie Marlinspike")
        }
    }

    scm {
        setUrl("scm:git@github.com:WhisperSystems/libsignal-android.git")
        setConnection("scm:git@github.com:WhisperSystems/libsignal-android.git")
        setDeveloperConnection("scm:git@github.com:WhisperSystems/libsignal-android.git")
    }
}

        }
    }

    repositories {
        maven {
            url = uri(getReleaseRepositoryUrl())
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
    }
}

signing {
    required { isReleaseBuild() }
    sign publishing.publications.mavenJava
}
