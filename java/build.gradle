plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.5'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

version = version_number
group = group_info

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    main {
        proto {
            srcDir '../protobuf'
        }
    }
    test {
        java {
            srcDirs = ['src/test/java/', project(':tests').file('src/test/java')]
        }
    }
}

dependencies {
    implementation "org.whispersystems:curve25519-java:${curve25519_version}"
    implementation 'com.google.protobuf:protobuf-java:3.23.4'

    testImplementation 'junit:junit:3.8.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.31.1'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

tasks.withType(Jar).configureEach {
    archiveBaseName.set("ddd-signal-protocol-java")
}

tasks.register('packageJavadoc', Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

tasks.register('packageSources', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives(tasks.named('packageJavadoc'))
    archives(tasks.named('packageSources'))
}

def isReleaseBuild() {
    !version.toString().contains("SNAPSHOT")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact tasks.named('packageJavadoc')
            artifact tasks.named('packageSources')

            groupId = "net.discdd"
            artifactId = "ddd-signal-protocol-java"
            version project.version

            pom {
                setName("ddd-signal-protocol-java")
                setDescription("DDD Signal Protocol cryptography library for Java")
                setUrl("https://github.com/SJSU-CS-systems-group/DDD-libsignal-protocol-java")

                licenses {
                    license {
                        setName("GPLv3")
                        setUrl("https://www.gnu.org/licenses/gpl-3.0.txt")
                        setDistribution("repo")
                    }
                }

                developers {
                    developer {
                        setName("Moxie Marlinspike")
                    }
                }

                scm {
                    setUrl("scm:git@github.com:SJSU-CS-systems-group/DDD-libsignal-protocol-java.git")
                    setConnection("scm:git@github.com:SJSU-CS-systems-group/DDD-libsignal-protocol-java.git")
                    setDeveloperConnection("scm:git@github.com:SJSU-CS-systems-group/DDD-libsignal-protocol-java.git")
                }
            }

        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SJSU-CS-systems-group/DDD")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

signing {
    required { isReleaseBuild() }
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications.mavenJava
}
